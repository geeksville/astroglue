############################################
#
# Script for Siril 1.4
# December 2024
# (C) Cyril Richard
# ExtractHaOIII v1.5
#
########### PREPROCESSING SCRIPT ###########
#
# Script for color camera preprocessing that
# extracts Ha and OIII
#
# Needs 4 sets of RAW images in the working
# directory, within 4 directories:
#   biases/
#   flats/
#   darks/
#   lights/
# Saves masters to ./masters/
#
############################################

requires 1.3.4

# Convert Bias Frames to .fit files
cd biases
convert bias -out=../process
cd ../process

# Stack Bias Frames to bias_stacked.fit
stack bias rej 3 3 -nonorm -out=../masters/bias_stacked
cd ..

# Convert Flat Frames to .fit files
cd flats
convert flat -out=../process
cd ../process

# Calibrate Flat Frames
calibrate flat -bias=../masters/bias_stacked

# Stack Flat Frames to pp_flat_stacked.fit
stack pp_flat rej 3 3 -norm=mul -out=../masters/pp_flat_stacked
cd ..

# Convert Dark Frames to .fit files
# kevinh: not needed - I just use bias frames instead
#cd darks
#convert dark -out=../process
#cd ../process

# Stack Dark Frames to dark_stacked.fit
#stack dark rej 3 3 -nonorm -out=../masters/dark_stacked
#cd ..

# Convert Light Frames to .fit files
cd lights
convert light -out=../process
cd ../process

# Calibrate Light Frames
# calibrate light -dark=../masters/dark_stacked -flat=../masters/pp_flat_stacked -cc=dark -cfa -equalize_cfa
calibrate light -bias=../masters/bias_stacked -flat=../masters/pp_flat_stacked -cfa -equalize_cfa

# Remove background gradient on a per-frame basis
seqsubsky pp_light 1

# FIXME, possibly do https://siril.org/tutorials/tuto-manual/#platesolving-reference-image at this step?

# Extract Ha and OIII
seqextract_HaOIII bkg_pp_light -resample=ha

# Align Ha lights
register Ha_bkg_pp_light

# FIXME - change rejection sigmas to be more selective?

# Stack calibrated Ha lights to Ha_stack (temporary)
stack r_Ha_bkg_pp_light rej 3 3 -norm=addscale -output_norm -32b -out=results_00001

# and flip if required
mirrorx_single results_00001

# Align OIII lights
register OIII_bkg_pp_light

# Stack calibrated OIII lights to OIII_stack (temporary)
stack r_OIII_bkg_pp_light rej 3 3 -norm=addscale -output_norm -32b -out=results_00002

# and flip if required
mirrorx_single results_00002

# Align the result images, small shifts and chromatic aberrations can occur
register results -transf=shift -interp=none

# Renorm OIII to Ha using PixelMath
pm $r_results_00002$*mad($r_results_00001$)/mad($r_results_00002$)-mad($r_results_00001$)/mad($r_results_00002$)*median($r_results_00002$)+median($r_results_00001$)
save ../results/result_OIII_$LIVETIME:%d$s

# Save Ha final result
load r_results_00001
save ../results/result_Ha_$LIVETIME:%d$s

# Remaining interactive workflow per https://www.youtube.com/watch?v=fDhOrKvM7GU
# and https://www.cloudynights.com/topic/943109-osc-processing-hasii-narrowband-filters-with-siril/?p=13777856

# Do background extraction on each of the two output files
# manually place the sample points, not very many pints needed (only about 8) - don't place near edges

# Go to "Pixel Math" and add the two files with plus icon
# Ha on Red
# Oii on B
# G = 0.6*Ha+0.4*Oii

# then crop

# then Color Calibration (see video)

# then remove stars with starnet

# then gen-hyp-stretch, but change to modifiedarcsin+evenweight (see video)

close
